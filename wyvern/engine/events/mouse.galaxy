//=========================================================
//		Wyvern Roleplaying Engine
//		Copyright (c) 2014 , Xethyr.407 [NA] / @90-proof
//=========================================================
// This software is distributed under the MIT License:
// <http://opensource.org/licenses/MIT>
//=========================================================

bool libwyv_mouse_moved(bool testConds, bool runActions) {
	// update mouse in world position
	libwyv_player[EventPlayer()].mouse_position = Point(EventMouseMovedPosXWorld(), EventMouseMovedPosYWorld());

	// update mouse in UI position, emit signals if the position in
	// Y plane (mouse up/down) increases or decreases
	if( EventMouseMovedPosYUI() > PointGetY(libwyv_player[EventPlayer()].mouse_position_ui) ) {
		TriggerSendEvent(LIBWYV_SIG_MOUSE_Y_INC + " " + IntToString(EventPlayer()));
	} else {
		TriggerSendEvent(LIBWYV_SIG_MOUSE_Y_DEC + " " + IntToString(EventPlayer()));
	}
	libwyv_player[EventPlayer()].mouse_position_ui = Point(EventMouseMovedPosXUI(), EventMouseMovedPosYUI());

	// debug
	//libwyv_print_output("x,y = " + FixedToString(EventMouseMovedPosXUI(), 1) + "," + FixedToString(EventMouseMovedPosYUI(), 1), EventPlayer());

	// update spawn splat
	if( libwyv_player[EventPlayer()].is_placing_unit  ) {
		libwyv_spawn_splat_update_pos();
	}

	return true;
}

bool librtpr_mouse_left_clicked_down(bool testConds, bool runActions) {
	libwyv_player[EventPlayer()].mouse_position = Point(EventMouseClickedPosXWorld(), EventMouseClickedPosYWorld());

	if( libwyv_player[EventPlayer()].is_placing_unit ) {
		libwyv_unit_create( UnitGetType(libwyv_player[EventPlayer()].spawn_splat),
							EventPlayer(),
							UnitGetPosition(libwyv_player[EventPlayer()].spawn_splat),
							UnitGetFacing(libwyv_player[EventPlayer()].spawn_splat)
						  );
	}

	return true;
}

// librtpr_mouse_right_clicked_
// =======================
// toggles player right mouse button down/up flag
bool librtpr_mouse_right_clicked_down(bool testConds, bool runActions) {
	libwyv_player[EventPlayer()].mouse_right = true;
	return true;
}
bool librtpr_mouse_right_clicked_up(bool testConds, bool runActions) {
	libwyv_player[EventPlayer()].mouse_right = false;
	return true;
}
