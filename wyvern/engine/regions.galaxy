//=========================================================
//		Wyvern Roleplaying Engine
//		Copyright (c) 2014 , Xethyr.407 [NA] / @90-proof
//=========================================================
// This software is distributed under the MIT License:
// <http://opensource.org/licenses/MIT>
//=========================================================

// global data table region identifier prefixes
const string LIBWYV_REGION_DATA_TABLE_IDENTIFIER = "reg";
const string LIBWYV_REGION_ID_DATA_TABLE_IDENTIFIER = "reg";
const string LIBWYV_REGION_LINK_DATA_TABLE_IDENTIFIER = "reg_link";
const string LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER = "reg_unit"; // for #attach

const string LIBWYV_WAYGATE_DATA_TABLE_IDENTIFIER = "waygate";
const string LIBWYV_WAYGATE_DIALOG_DATA_TABLE_IDENTIFIER = "waygate_diag";
const string LIBWYV_WAYGATE_DIALOG_REG_DATA_TABLE_IDENTIFIER = "waygate_diag_reg"; //region from dialog
const string LIBWYV_WAYGATE_ANCHOR_DATA_TABLE_IDENTIFIER = "waygate_diag_unit";

// the dummy unit type used to anchor the waygate dialog "ENTER" button
// (actual declaration occurs in data.galaxy, due to include order issues)
// const string LIBWYV_WAYGATE_DIALOG_ANCHOR_UNIT = "Beacon_Zerg";

// region teleport counter
// (must start at one because unit custom val defaults to 0)
int libwyv_region_waygate_counter = 1;

// libwyv_region_get_name
// ==================
// Regions are stored in global data table as:
// "reg [player number] [name]", e.g. "reg 1 R"
string libwyv_region_get_name(string name, int player) {
	return (LIBWYV_REGION_DATA_TABLE_IDENTIFIER + " " + IntToString(player) + " " + StringCase(name, false));
}

// libwyv_region_get_public_name
// ==================
// Returns the public (user inputted) name for the region
// (the third word in the region data table name)
string libwyv_region_get_public_name(string data_table_name) {
	return StringWord(data_table_name, 3);
}

// libwyv_region
// ===============
// Returns the region associated with the input PUBLIC NAME for a given
// player. If the region does not exist, returns null.
region libwyv_region(string name, int player) {
	region r = null;
	string rgn_dtable_name = libwyv_region_get_name(name, player);
	if( DataTableValueExists(true, rgn_dtable_name) ) {
		r = DataTableGetRegion(true, rgn_dtable_name);
	}
	return r;
}

// libwyv_create_waygate_dialogs
// =====================
// creates a dummy unit to act as an anchor for the waygate dialog. the unit
// is invisible to all players and simply acts as a reference actor for
// attaching the "Enter" dialog button.
void libwyv_create_waygate_dialog(string rgn_name) {
	point p = RegionGetCenter( DataTableGetRegion(true, rgn_name) );
	int dialog = libwyv_create_dialog(90, 44, 0, 0, 0); // create the waygate dialog
	unit anchor;
	int i = 1;
	libwyv_create_dialog_btn(dialog, 90, 44, c_anchorCenter, 0, 0, "Click to teleport these unit(s).", "Enter");
	DataTableSetDialog(true, LIBWYV_WAYGATE_DIALOG_DATA_TABLE_IDENTIFIER + " " + rgn_name, dialog);
	DataTableSetString(true, LIBWYV_WAYGATE_DIALOG_REG_DATA_TABLE_IDENTIFIER + " " + IntToString(dialog), rgn_name);

	// anchor the waygate dialog onto the unit
	// first check if a region is attached to a unit:
	if( DataTableValueExists(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + rgn_name) ) {
		anchor = DataTableGetUnit(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + rgn_name);
		if( anchor != null && UnitIsAlive(anchor) ) { // attach waygate dialog to region's unit anchor
			UnitSetCustomValue(anchor, LIBWYV_UNIT_INDEX_WAYGATE_DIALOG, IntToFixed(dialog));
			DialogSetPositionRelativeToUnit(dialog, anchor, "Ref_Origin", -60, 0);
			DialogSetVisible(dialog, PlayerGroupAll(), true);
			return;
		}
		else { // anchor == null, unit doesn't exist, e.g. unit has been killed/removed
			DataTableValueRemove(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + rgn_name);
		}
	}
	// else, create a static unit anchor on ground
	UnitCreate(1, LIBWYV_WAYGATE_DIALOG_ANCHOR_UNIT, c_unitCreateIgnorePlacement, 0, p, 270.0);
	//ActorSend(libNtve_gf_MainActorofUnit(UnitLastCreated()), "SetOpacity {0.1}"); // set opacity to hide
	// hide the beacons for all players
	for( i = 1; i < LIBWYV_MAX_PLAYERS; i += 1 ) {
		ActorSend(libNtve_gf_MainActorofUnit(UnitLastCreated()), "SetFilterPlayers " + IntToString(i));
	}
	UnitSetCustomValue(UnitLastCreated(), LIBWYV_UNIT_INDEX_WAYGATE_ANCHOR, 1.0);
	DataTableSetUnit(true, LIBWYV_WAYGATE_ANCHOR_DATA_TABLE_IDENTIFIER + " " + rgn_name, UnitLastCreated());

	DialogSetPositionRelativeToUnit(dialog, UnitLastCreated(), "Ref_Overhead", -60, 0);
	//DialogSetVisible(dialog, PlayerGroupAll(), true);
}

// libwyv_link_regions
// =================
// region links involve 2 data table parameters:
// -> [region_name_from_ID] (string) : links a waygate by ID to a region name (for quick region check via loop)
// -> [name_to_linked_region_id] (int) : stores the [region_name_from_ID] (an integer ID) of the linked region
// (presumes input region names already in proper format with libwyv_region_get_name)
void libwyv_link_regions(string rgn_name_1, string rgn_name_2) {
	// create a link from region_1 to region_2
	DataTableSetString(true, LIBWYV_WAYGATE_DATA_TABLE_IDENTIFIER + " " + IntToString(libwyv_region_waygate_counter), rgn_name_1);
	DataTableSetInt(true, LIBWYV_REGION_LINK_DATA_TABLE_IDENTIFIER + " " + rgn_name_1, libwyv_region_waygate_counter + 1);
	libwyv_region_waygate_counter += 1;

	// create a link from region 2 to region 1
	DataTableSetString(true, LIBWYV_WAYGATE_DATA_TABLE_IDENTIFIER + " " + IntToString(libwyv_region_waygate_counter), rgn_name_2);
	DataTableSetInt(true, LIBWYV_REGION_LINK_DATA_TABLE_IDENTIFIER + " " + rgn_name_2, libwyv_region_waygate_counter - 1);
	libwyv_region_waygate_counter += 1;

	libwyv_create_waygate_dialog(rgn_name_1);
	libwyv_create_waygate_dialog(rgn_name_2);
}

// libwyv_region_remove_links
// =====================
// checks if there are linked regions, from a region name,
// and remove links if exist
void libwyv_region_remove_links(string rgn1_name, int player) {
	string rgn2_name;
	int rgn_2_id;
	int reg1_dialog;
	int reg2_dialog;

	if( DataTableValueExists(true, LIBWYV_REGION_LINK_DATA_TABLE_IDENTIFIER + " " + rgn1_name) ) {
		rgn_2_id = DataTableGetInt(true, LIBWYV_REGION_LINK_DATA_TABLE_IDENTIFIER + " " + rgn1_name);
		rgn2_name = DataTableGetString(true, LIBWYV_WAYGATE_DATA_TABLE_IDENTIFIER + " " + IntToString(rgn_2_id) );

		// remove the region linking data
		DataTableValueRemove(true, LIBWYV_REGION_LINK_DATA_TABLE_IDENTIFIER + " " + rgn2_name);
		DataTableValueRemove(true, LIBWYV_WAYGATE_DATA_TABLE_IDENTIFIER + " " + IntToString(rgn_2_id));

		DataTableValueRemove(true, LIBWYV_REGION_LINK_DATA_TABLE_IDENTIFIER + " " + rgn1_name);
		DataTableValueRemove(true, LIBWYV_WAYGATE_DATA_TABLE_IDENTIFIER + " " + IntToString(rgn_2_id - 1));

		// remove associated waygate dialogs and unit anchors
		reg1_dialog = DataTableGetDialog(true, LIBWYV_WAYGATE_DIALOG_DATA_TABLE_IDENTIFIER + " " + rgn1_name);
		reg2_dialog = DataTableGetDialog(true, LIBWYV_WAYGATE_DIALOG_DATA_TABLE_IDENTIFIER + " " + rgn2_name);
		DialogDestroy(reg1_dialog);
		DialogDestroy(reg2_dialog);
		DataTableValueRemove(true, LIBWYV_WAYGATE_DIALOG_REG_DATA_TABLE_IDENTIFIER + " " + IntToString(reg1_dialog));
		DataTableValueRemove(true, LIBWYV_WAYGATE_DIALOG_REG_DATA_TABLE_IDENTIFIER + " " + IntToString(reg2_dialog));

		// remove waygate static anchor units
		if( DataTableValueExists(true, LIBWYV_WAYGATE_ANCHOR_DATA_TABLE_IDENTIFIER + " " + rgn1_name) ) {
			UnitRemove(DataTableGetUnit(true, LIBWYV_WAYGATE_ANCHOR_DATA_TABLE_IDENTIFIER + " " + rgn1_name));
		}
		if( DataTableValueExists(true, LIBWYV_WAYGATE_ANCHOR_DATA_TABLE_IDENTIFIER + " " + rgn2_name) ) {
			UnitRemove(DataTableGetUnit(true, LIBWYV_WAYGATE_ANCHOR_DATA_TABLE_IDENTIFIER + " " + rgn2_name));
		}

		// reset custom value (for waygate dialog id) for units that had waygate attached
		if( DataTableValueExists(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + rgn1_name) ) {
			UnitSetCustomValue(DataTableGetUnit(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + rgn1_name), LIBWYV_UNIT_INDEX_WAYGATE_DIALOG, 0);
		}
		if( DataTableValueExists(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + rgn2_name) ) {
			UnitSetCustomValue(DataTableGetUnit(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + rgn2_name), LIBWYV_UNIT_INDEX_WAYGATE_DIALOG, 0);
		}
	}
}
