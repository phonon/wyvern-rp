//=========================================================
//		Exile Roleplaying Engine
//		Copyright (c) 2014 , Xethyr.407 [NA] / @90-proof
//=========================================================
// This software is distributed under the MIT License:
// <http://opensource.org/licenses/MIT>
//=========================================================

// creates, removes key/values from a section in player bank
// !!! BankSave required to ensure the changes persist
void libxil_bank_create(string section, string key, string content, int player) {
	BankValueSetFromString(libxil_player[player].bank_storage, section, key, content);
	BankSave(libxil_player[player].bank_storage);
}

void libxil_bank_remove(string section, string key, int player) {
	BankKeyRemove(libxil_player[player].bank_storage, section, key);
	BankSave(libxil_player[player].bank_storage);
}

void libxil_bank_section_remove_all(string section, int player) {
	BankSectionRemove(libxil_player[player].bank_storage, section);
	BankSave(libxil_player[player].bank_storage);
}

// libxil_bank_key_content_dump
// =================
// prints out content of bank value
void libxil_bank_key_content_dump(string section, string key, int player) {
	if( BankKeyExists(libxil_player[player].bank_storage, section, key) ) {
		libxil_print_output(StringCase(section, true) + ": " + key, player);
		libxil_print_output("CONTENT: " + BankValueGetAsString(libxil_player[player].bank_storage, section, key), player);
	}
	else {
		libxil_print_error(StringCase(section, true) + " " + key + " DOES NOT EXIST", player);
	}
}

// libxil_bank_section_dump
// =================
// lists all of a player's aliases/templates
void libxil_bank_section_dump(string section, int player) {
	int count = BankKeyCount(libxil_player[player].bank_storage, section);
	string keys = "[";

	if( count ) {
		while(count) {
			keys = keys + "\'" + BankKeyName(libxil_player[player].bank_storage, section, count - 1) + "\',";
			count -= 1;
		}
		// remove extra ','
		keys = StringSub(keys, 1, StringLength(keys) - 1) + "]";
		libxil_print_output(StringCase(section, true) + ":\n" + keys, player);
	}
	else {
		libxil_print_output("You have no " + section, player);
	}
}
