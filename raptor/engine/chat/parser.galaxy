//=========================================================
//		Raptor Roleplaying Engine
//		Copyright (c) 2014 , Xethyr.407 [NA] / @90-proof
//=========================================================
// This software is distributed under the MIT License:
// <http://opensource.org/licenses/MIT>
//=========================================================

// librptr_chat_parse
// =====================
// iteratively parses a string of commands
// reserved special cases are for "var", "alias", and "template"
const int LIBRPTR_CHAT_PARSER_RECURSION_LIMIT = 10;
const string LIBRPTR_CHAT_COMMAND_DATA_TABLE_PREFIX = "librptr_command_";

void librptr_chat_command_create(string command, string function) {
	trigger t = TriggerCreate(function);
	if (t == null) {
		librptr_print("FAILED TO LOAD COMMAND: " + command, PlayerGroupAll());
	} else {
		DataTableSetTrigger(true, LIBRPTR_CHAT_COMMAND_DATA_TABLE_PREFIX + command, t);
		
		librptr_print("REGISTERED COMMAND: " + command, PlayerGroupAll() );
		
		librptr_settings.commands = librptr_settings.commands + command + ", ";
	}
}

void librptr_chat_parse(string msg, int player, unitgroup target, int recursion_depth) {
	string current = msg;
	string current_alias_subbed;
	int semicolon_index;
	
	while ( msg != "" ) {
		// update player's chat_msg current cache
		librptr_player[player].chat_msg = msg;
		
		// get a chunk of the msg by finding ";" separater
		semicolon_index = StringFind(msg, ";", c_stringNoCase);
		//librptr_print_success("[INDEX]:" + IntToString(semicolon_index));
		if (semicolon_index) {
			current = librptr_string_trim( StringSub(msg, 1, semicolon_index - 1) );
			msg = StringSub( msg, semicolon_index + 1, StringLength(msg) );
		}
		else {
			current = msg;
			msg = "";
		}
		
		// check if performing reserved keyword commands (alias, template, var)
		if ( StringEqual( StringWord(current, 1), "alias", c_stringNoCase) ) {
			// returns true if using "alias create":
			// in this case, the rest of msg is used up AS IS, so parsing considered finished
			if( librptr_alias_command_redirect(current, player) ) { 
				break;
			}
			continue; // skip OOC printing
		}
		else if ( StringEqual( StringWord(current, 1), "template", c_stringNoCase) ) {
			// returns true if using "template create":
			// in this case, the rest of msg is used up AS IS, so parsing considered finished
			if( librptr_template_command_redirect(current, player) ) { 
				break;
			}
			continue; // skip OOC printing
		}
		else if ( StringEqual( StringWord(current, 1), "var", c_stringNoCase) ) {
			// TODO: ADD VARIABLE SYSTEM
		}
		
		// replace aliases (returns true if aliases):
		// if aliases subbed, affix to front of msg and reparse 
		current_alias_subbed = librptr_alias_sub(current, player, 0);
		if( current_alias_subbed != current ) {
			msg = current_alias_subbed + ";" + msg;
			continue;
		}
		
		// if template exists, append evaluated template to msg and parse
		if( BankKeyExists( librptr_player[player].bank_storage, LIBRPTR_BANK_SECTION_TEMPLATE, StringWord(current, 1) ) ) {
			msg = librptr_template(current, player) + ";" + msg;
			continue;
		}
		
		
		// TODO: replace variables

		
		// run command (if exists and permissions for player valid)
		if( DataTableValueExists(true, LIBRPTR_CHAT_COMMAND_DATA_TABLE_PREFIX + StringWord(current, 1) ) ) {
			
			// if does, run wrapper function to run a command (does permissions checks etc)
			librptr_print_success("RUNNING COMMAND: " + StringWord(current, 1), player);
			
			librptr_player[player].chat_msg = current;
			TriggerExecute( DataTableGetTrigger(true, LIBRPTR_CHAT_COMMAND_DATA_TABLE_PREFIX + StringWord(current, 1) ), false, true);
		}
		else if ( !librptr_player[player].muted ) {
			librptr_chat_print_OOC( current, player );
		}
		
		Wait(0.0, c_timeGame); // speeds up loop processing
	}
}

