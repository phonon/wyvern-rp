//=========================================================
//		Raptor Roleplaying Engine
//		Copyright (c) 2014 , Xethyr.407 [NA] / @90-proof
//=========================================================
// This software is distributed under the MIT License:
// <http://opensource.org/licenses/MIT>
//=========================================================

// creates, removes key/values from a section in player bank
// !!! BankSave required to ensure the changes persist
void librptr_bank_create(string section, string key, string content, int player) {
	BankValueSetFromString(librptr_player[player].bank_storage, section, key, content);
	BankSave(librptr_player[player].bank_storage);
}

void librptr_bank_remove(string section, string key, int player) {
	BankKeyRemove(librptr_player[player].bank_storage, section, key);
	BankSave(librptr_player[player].bank_storage);
}

void librptr_bank_section_remove_all(string section, int player) {
	BankSectionRemove(librptr_player[player].bank_storage, section);
	BankSave(librptr_player[player].bank_storage);
}

// librptr_bank_key_content_dump
// =================
// prints out content of bank value
void librptr_bank_key_content_dump(string section, string key, int player) {
	if( BankKeyExists(librptr_player[player].bank_storage, section, key) ) {
		librptr_print_output(StringCase(section, true) + ": " + key, player);
		librptr_print_output("CONTENT: " + BankValueGetAsString(librptr_player[player].bank_storage, section, key), player);
	}
	else {
		librptr_print_error(StringCase(section, true) + " " + key + " DOES NOT EXIST", player);
	}
}

// librptr_bank_section_dump
// =================
// lists all of a player's aliases/templates
void librptr_bank_section_dump(string section, int player) {
	int count = BankKeyCount(librptr_player[player].bank_storage, section);
	string keys = "[";
	
	if( count ) {
		while(count) {
			keys = keys + "\'" + BankKeyName(librptr_player[player].bank_storage, section, count - 1) + "\',";
			count -= 1;
		}
		// remove extra ','
		keys = StringSub(keys, 1, StringLength(keys) - 1) + "]";
		librptr_print_output(StringCase(section, true) + ":\n" + keys, player);
	}
	else {
		librptr_print_output("You have no " + section, player);
	}
}