//=========================================================
//		Wyvern Roleplaying Engine
//		Copyright (c) 2014 , Xethyr.407 [NA] / @90-proof
//=========================================================
// This software is distributed under the MIT License:
// <http://opensource.org/licenses/MIT>
//=========================================================

// libwyv_player_leaves
// ==================
// print message "[Player] has left the game"
// -> if player is admin, must promote a new admin
bool libwyv_player_leaves(bool testConds, bool runActions) {
	int player = 1;
	playergroup active_players = PlayerGroupActive();
	
	libwyv_print_text( libwyv_player_name_colored(EventPlayer()) + StringToText(" has left the game"), PlayerGroupAll() );

	// if admin leaves, make another player the admin
	if( libwyv_player[EventPlayer()].permission_level == LIBWYV_PERMISSIONS_LEVEL_ADMIN ) {
		// demote player that left
		libwyv_player[EventPlayer()].permission_level = LIBWYV_PERMISSIONS_LEVEL_DEFAULT;
		
		// promote first moderator detected
		while( player < LIBWYV_MAX_PLAYERS ) {
			if ( libwyv_player[player].permission_level == LIBWYV_PERMISSIONS_LEVEL_MOD ) {
				libwyv_player[player].permission_level = LIBWYV_PERMISSIONS_LEVEL_ADMIN;
				libwyv_print_text( libwyv_player_name_colored(player) + StringToText(" has been promoted to admin"), PlayerGroupAll() );
				return true;
			}
			player += 1;
		}
		
		// otherwise promote random player (if there are players left)
		if( PlayerGroupCount(active_players) == 0 ) {
			return true;
		}
		player = PlayerGroupPlayer( active_players, RandomInt(1, PlayerGroupCount(active_players)) );
		libwyv_player[player].permission_level = LIBWYV_PERMISSIONS_LEVEL_ADMIN;
		libwyv_print_text( libwyv_player_name_colored(player) + StringToText(" has been promoted to admin"), PlayerGroupAll() );
	}
	// also demote any moderators
	else if( libwyv_player[EventPlayer()].permission_level == LIBWYV_PERMISSIONS_LEVEL_MOD ) {
		libwyv_player[EventPlayer()].permission_level = LIBWYV_PERMISSIONS_LEVEL_DEFAULT;
	}
	
	return true;
}
