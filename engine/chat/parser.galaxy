//=========================================================
//		Wyvern Roleplaying Engine
//		Copyright (c) 2014 , Xethyr.407 [NA] / @nmosfet
//=========================================================
// This software is distributed under the MIT License:
// <http://opensource.org/licenses/MIT>
//=========================================================

include "wyvern/engine/chat/alias.galaxy"
include "wyvern/engine/chat/macro.galaxy"
include "wyvern/engine/chat/variables.galaxy"

const string LIBWYV_CHAT_COMMAND_DATA_TABLE_PREFIX = "libwyv_command_";

// libwyv_arg
// ===============
// Returns the string argument value at the index.
// Wrapper for StringWord in order to appear more descriptive in commands.
string libwyv_arg(string argv, int index) {
	return StringWord(argv, index);
}

// function signature for wyvern chat commands
void libwyv_command_signature(int user, unitgroup u, string argv);

// funcref type for wyvern commands
typedef funcref<libwyv_command_signature> libwyv_command_type;

// Table of all wyvern commands:
// ===========================
// Commands are created and hardcoded/registered as an entry in the table.
// Global data table maps a function name to an index in this array:
//
//   DataTable index -> commands[index] -> command funcref
//
// Though this is an annoying double leap (map -> array -> func), easier
// to reference a command in the table instead of creating an if-elif cascade
// to match a function string name to its function reference
//
// Array length hardcoded such that num of commands registered should never
// reach command table size. If it does, increase this array size.
libwyv_command_type[100] libwyv_command_table;

// Counter for the next free array entry in the libwyv_command_table array
int libwyv_command_table_counter = 0;

// libwyv_chat_command_create
// ==================
// Adds a command function to the global array of commands.
// Use data table to map command name (string) to the array index.
void libwyv_chat_command_create(string command, libwyv_command_type func) {
	DataTableSetInt(true, LIBWYV_CHAT_COMMAND_DATA_TABLE_PREFIX + command, libwyv_command_table_counter);
	libwyv_command_table[libwyv_command_table_counter] = func;
	libwyv_command_table_counter += 1;

	// add command to list of commands
	libwyv_settings.commands = libwyv_settings.commands +  ", " + command;
}

// Max iterations for number of aliases and macros replaced by parser
// per parser function call. This is to break from infinite loops caused
// by recursive aliases or macros.
const int LIBWYV_PARSE_LIMIT = 1000;

// libwyv_chat_parse
// =================
// parses a chat message string for commands (each command separated by ";" )
// built-in commands are "alias", "macro", and "var" which affect parsing
// hence these are treated separately than all other commands
void libwyv_chat_parse(string msg, int player, unitgroup target) {
	string current = msg;
	string current_alias_subbed;
	string cmd_key;
	int count = 0;
	int semicolon_index;
	int next_semicolon_index;
	libwyv_command_type func; // command ref test

	libwyv_player[player].context = target;

	while ( msg != "" && count < LIBWYV_PARSE_LIMIT) {
		// update player's chat_msg current cache
		libwyv_player[player].chat_msg = msg;

		// get a chunk of the msg by finding ";" separater
		// -> to allow players to still use ";" in chat, "\;" is parsed as an escape sequence
		// -> "\;" will print out a ";" in the chat, so we must find "\;" in a message
		semicolon_index = StringFind(msg, ";", c_stringNoCase);
		while( true ) {
			if( semicolon_index ) {
				// if this is an escaped semicolon "\;", find the next semicolon
				if( StringSub(msg, semicolon_index - 1, semicolon_index - 1) == "\\" ) {
					next_semicolon_index = StringFind( StringSub( msg, semicolon_index + 1, StringLength(msg) ), ";", c_stringNoCase );
					if( next_semicolon_index > 0 ) {
						// must add because next_semicolon_index is relative to a substring
						semicolon_index = semicolon_index + next_semicolon_index;
						continue;
					}
					else { // at end of msg
						current = msg;
						msg = "";
						break;
					}
				}
				else {
					current = libwyv_string_trim( StringSub(msg, 1, semicolon_index - 1) );
					msg = StringSub( msg, semicolon_index + 1, StringLength(msg) );
					break;
				}
			}
			else { // at end of msg
				current = msg;
				msg = "";
				break;
			}

		}
		// after getting current, replace escape sequence for semicolon: "\;" -> ";"
		current = StringReplaceWord(current, "\\;", ";", c_stringReplaceAll, false);

		// go no further with parsing if current string is empty
		// (this occurs if player types a string of ";;;;;")
		if( current == "" ) {
			continue;
		}

		// check if performing reserved keyword commands (alias, macro, var)
		if ( StringEqual( StringWord(current, 1), "alias", c_stringNoCase) ) {
			// returns true if using "alias create":
			// in this case, the rest of msg is used up AS IS, so parsing considered finished
			if( libwyv_alias_command_redirect(current, player) ) {
				break;
			}
			continue; // skip OOC printing
		}
		else if ( StringEqual( StringWord(current, 1), "macro", c_stringNoCase) ) {
			// returns true if using "macro create":
			// in this case, the rest of msg is used up AS IS, so parsing considered finished
			if( libwyv_macro_command_redirect(current, player) ) {
				break;
			}
			continue; // skip OOC printing
		}
		else if ( StringEqual( StringWord(current, 1), "var", c_stringNoCase) ) {
			// TODO: ADD VARIABLE SYSTEM
		}

		// replace aliases (returns true if aliases):
		// if aliases subbed, affix to front of msg and reparse
		current_alias_subbed = libwyv_alias_sub(current, player, 0);
		if( current_alias_subbed != current ) {
			msg = current_alias_subbed + ";" + msg;
			count += 1;
			continue;
		}

		// if macro exists, append evaluated macro to msg and parse
		if( BankKeyExists( libwyv_player[player].bank_storage, LIBWYV_BANK_SECTION_MACRO, StringWord(current, 1) ) ) {
			msg = libwyv_macro(current, player) + ";" + msg;
			count += 1;
			continue;
		}

		// TODO: replace variables


		// run command (if exists and permissions for player valid)
		cmd_key = LIBWYV_CHAT_COMMAND_DATA_TABLE_PREFIX + StringCase(StringWord(current, 1), false);
		if ( DataTableValueExists(true, cmd_key) ) {
			func = libwyv_command_table[ DataTableGetInt(true, cmd_key) ];
			target = libwyv_player[player].context;
			func( player, target, current );
		}
		else if ( !libwyv_player[player].muted ) {
			libwyv_chat_print_OOC(player, current);
		}
	}

	// reached parse limit
	if( count == LIBWYV_PARSE_LIMIT ) {
		libwyv_print_error(player, "Reached parse limit (recursive alias or recursive macro?)");
	}

}
