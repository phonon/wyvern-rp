//=========================================================
//		Wyvern Roleplaying Engine
//		Copyright (c) 2014 , Xethyr.407 [NA] / @nmosfet
//=========================================================
// This software is distributed under the MIT License:
// <http://opensource.org/licenses/MIT>
//=========================================================

// libwyv_command_spawn
// =======================
// Deals with redirecting the @spawn command to a proper unit create command
// all actual unit creation is handled in engine/unit.galaxy
void libwyv_command_spawn(int user, unitgroup u, string argv) {
	point spawn_point = libwyv_player[user].mouse_position;
	int count = StringToInt( StringWord(argv, 2) );
	string type = StringWord(argv, 3);
	region location = null;
	int player = user;
	string parameter; 	// used for parsing a location or player parameter

	// if no parameters specified, check last instance and try to repeat
	if( StringWord(argv, 2) == "" ) {
		if( libwyv_player[user].spawn_instance_type != "" ) {
			type = libwyv_player[user].spawn_instance_type;
			count = libwyv_player[user].spawn_instance_count;
			player = libwyv_player[user].spawn_instance_player;
			if( libwyv_player[user].spawn_instance_location != null ) {
				spawn_point = RegionGetCenter( libwyv_player[user].spawn_instance_location );
			}

			libwyv_player[user].context = UnitGroupEmpty();
			while (count > 0) {
				libwyv_unit_create(type, player, spawn_point, 270.0);
				count -= 1;
			}

			return;
		}
		else {
			libwyv_print_error(user, "Invalid unit type, or no previous spawn instance");
			return;
		}
	}

	// if count is 0, try checking 3rd index, swap unit type to 2nd index
	// (this allows either @spawn # unit or @spawn unit #)
	if( count == 0 ) {
		count = StringToInt( StringWord(argv, 3) );
		type = StringWord(argv, 2);
	}

	// get unit type
	if( type == "" ) {
		libwyv_print_error(user, "INVALID UNIT TYPE");
		return;
	}
	else {
		type = UnitTypeFromString(type);
		//type = librtpr_catalog_get( c_gameCatalogUnit, type );
		if (type == "" || !CatalogEntryIsValid(c_gameCatalogUnit, type) || UnitTypeTestFlag(type, c_unitFlagMissile) ) {
			libwyv_print_error(user, "INVALID UNIT TYPE");
			return;
		}
	}

	// if count is <= 0, then turn on mouse-click spawning
	if( count <= 0 ) {
		libwyv_print_output(user, "SPAWNING: " + type + "\n(Press [Left Mouse] to place, [ESC] key to cancel.\n" +
							"Hold: [Ctrl] to rotate, [Shift] to scale, [Alt] to change height,\n" +
							"Hold: [Right Mouse] to lock position)");
		libwyv_spawn_create_splat(type, user);
		libwyv_player[user].is_placing_unit = true;
		UISetSelectionTypeEnabled(PlayerGroupSingle(user), c_localSelectionTypeUnknown, false);
		UnitClearSelection(user);	// deselect player selected units
		return;
	}

	// otherwise spawn a number of units:

	// check if a player or region is specified (4th and 5th index)
	// -> region specified by "#[name]"
	// -> player specified by "@[player-color]"
	if( StringWord(argv, 4) != "" ) {
		if( StringContains( StringWord(argv, 4), "#", c_stringBegin, c_stringNoCase) ) {
			parameter = StringWord(argv, 4);
			parameter = libwyv_region_get_name( StringSub(parameter, 2, StringLength(parameter)), user );
			if( DataTableValueExists(true, parameter) ) {
				location = DataTableGetRegion(true, parameter);
				spawn_point = RegionGetCenter(location);
			}
			else {
				libwyv_print_error(user, "No region by name \"" + parameter + "\" exists, defaulting to cursor");
			}
		}
		else if( StringContains( StringWord(argv, 4), "@", c_stringBegin, c_stringNoCase) ) {
			parameter = StringWord(argv, 4);
			player = libwyv_color_to_player( StringSub(parameter, 2, StringLength(parameter)) );
			if( player == -1 ) {
				player = user;
				libwyv_print_error(user, "Invalid player color, defaulting to self");
			}
		}
	}
	if( StringWord(argv, 5) != "" ) {
		if( StringContains( StringWord(argv, 5), "#", c_stringBegin, c_stringNoCase) ) {
			parameter = StringWord(argv, 5);
			parameter = libwyv_region_get_name( StringSub(parameter, 2, StringLength(parameter)), user );
			if( DataTableValueExists(true, parameter) ) {
				location = DataTableGetRegion(true, parameter);
				spawn_point = RegionGetCenter(location);
			}
			else {
				libwyv_print_error(user, "No region by name \"" + parameter + "\" exists, defaulting to cursor");
			}
		}
		else if( StringContains( StringWord(argv, 5), "@", c_stringBegin, c_stringNoCase) ) {
			parameter = StringWord(argv, 5);
			player = libwyv_color_to_player( StringSub(parameter, 2, StringLength(parameter)) );
			if( player == -1 ) {
				player = user;
				libwyv_print_error(user, "Invalid player color, defaulting to self");
			}
		}
	}

	// ensure spawn instance count is less than max count in settings
	// TEMPORARY: until better spawn throttling is implemented
	if( count > libwyv_settings.max_spawn_count ) {
		count = libwyv_settings.max_spawn_count;
	}

	// set player spawn instance (before spawning so count is preserved)
	libwyv_player[user].spawn_instance_type = type;
	libwyv_player[user].spawn_instance_count = count;
	libwyv_player[user].spawn_instance_player = player;
	libwyv_player[user].spawn_instance_location = location;

	// make spawned units
	libwyv_player[user].context = UnitGroupEmpty();
	while (count > 0) {
		UnitGroupAddUnitGroup(libwyv_player[user].context, libwyv_unit_create(type, player, spawn_point, 270.0));
		count -= 1;
	}

	return;
}
