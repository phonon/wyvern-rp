//=========================================================
//		Wyvern Roleplaying Engine
//		Copyright (c) 2014 , Xethyr.407 [NA] / @90-proof
//=========================================================
// This software is distributed under the MIT License:
// <http://opensource.org/licenses/MIT>
//=========================================================

// libwyv_command_region_create
// ====================
// creates a region and sets it into the global data table
// regions are stored in format:
// "reg [player] [name]"
bool libwyv_command_region_create(bool testConds, bool runActions) {
	region r;
	fixed radius;
	string name = StringWord(libwyv_player[EventPlayer()].chat_msg, 2);

	if( name == "" ) {
		libwyv_print_error("Invalid region name", EventPlayer());
		return false;
	}

	radius = StringToFixed(StringWord(libwyv_player[EventPlayer()].chat_msg, 3));
	if( radius == 0.0 ) {
		radius = 2;
	}

	if( DataTableGetRegion( true, libwyv_region_get_name(name, EventPlayer()) ) ) {
		libwyv_print_error("A region with that name already exists!", EventPlayer());
		return false;
	}

	r = RegionCircle( libwyv_player[EventPlayer()].mouse_position, radius );

	DataTableSetRegion( true, libwyv_region_get_name(name, EventPlayer()), r );
	libwyv_print_output("Region (circle) \"" + name + "\" has been created.", EventPlayer());

	return true;
}

// libwyv_command_region_remove
// ==================
// removes a region and any linking to other regions
// (for linking, see libwyv_command_region_teleport below)
bool libwyv_command_region_remove(bool testConds, bool runActions) {
	string name = libwyv_region_get_name(StringWord(libwyv_player[EventPlayer()].chat_msg, 2), EventPlayer());
	region rgn = DataTableGetRegion( true, name );

	if( rgn == null ) {
		libwyv_print_error("Region does not exist", EventPlayer());
		return true;
	}

	// first remove any linking data (for teleports) related to this region
	libwyv_region_remove_links(name, EventPlayer());

	// remove region
	DataTableValueRemove( true, name );

	// if region attached to unit, remove unit anchor data table entry
	if( DataTableValueExists(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + name) ) {
		DataTableValueRemove(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + name);
	}

	libwyv_print_output("Region \"" + libwyv_region_get_public_name(name) + "\" has been removed", EventPlayer());

	return true;
}

// libwyv_command_region_removeall
// ======================
// removes all regions and links
bool libwyv_command_region_removeall(bool testConds, bool runActions) {
	int index = 1;
	int removed = 0;
	string rgn_identifier = libwyv_region_get_name("", EventPlayer());
	string name; // full region data table name

	while( index <= DataTableValueCount(true) ) {
		if( StringContains(DataTableValueName(true, index), rgn_identifier, c_stringBegin, c_stringNoCase) ) {
			name = DataTableValueName(true, index);
			libwyv_region_remove_links( name, EventPlayer() );
			DataTableValueRemove(true, name);
			// if region attached to unit, remove unit anchor data table entry
			if( DataTableValueExists(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + name) ) {
				DataTableValueRemove(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + name);
			}

			removed += 1;
		}
		index += 1;
	}

	if(removed > 0) {
		libwyv_print_output("All your regions have been removed", EventPlayer());
	}
	else {
		libwyv_print_output("You have no regions to remove", EventPlayer());
	}

	return true;
}
// libwyv_command_region_list
// ======================
// lists a player's regions from the data table
// because region creation is not case sensitive,
// these will always be in lowercase
bool libwyv_command_region_list(bool testConds, bool runActions) {
	int i = 1;
	string region_list = "";

	while( i < DataTableValueCount(true) ) {
		if( StringWord( DataTableValueName(true, i), 1 ) == LIBWYV_REGION_DATA_TABLE_IDENTIFIER &&
			StringToInt( StringWord( DataTableValueName(true, i), 2) ) == EventPlayer() ) {
			region_list = region_list + ", " + libwyv_region_get_public_name(DataTableValueName(true, i));
		}
		i += 1;
	}

	if( region_list != "" ) {
		region_list = StringSub(region_list, 3, StringLength(region_list)); // get rid of leading ", "
		libwyv_print_output("Your regions:\n" + region_list, EventPlayer());
	}
	else {
		libwyv_print_output("You have no regions", EventPlayer());
	}

	return true;
}

// libwyv_command_region_teleport
// =========================
// creates a "waygate" between two regions, allowing (any) player
// to press a dialog button to teleport selected units between
// regions. (see engine/regions.galaxy to see how regions are linked,
// events/region_waygates.galaxy to see how waygates function)
// (heavily modified from Cortex engine)
bool libwyv_command_region_teleport(bool testConds, bool runActions) {
	string rgn_1_name = libwyv_region_get_name(StringWord(libwyv_player[EventPlayer()].chat_msg, 2), EventPlayer());
	string rgn_2_name = libwyv_region_get_name(StringWord(libwyv_player[EventPlayer()].chat_msg, 3), EventPlayer());
	region rgn_1;
	region rgn_2;
	trigger t;

	if( !DataTableValueExists(true, rgn_1_name) || !DataTableValueExists(true, rgn_2_name) ) {
		libwyv_print_error("One or more invalid regions were entered.", EventPlayer());
		return true;
	}

	rgn_1 = DataTableGetRegion( true, rgn_1_name );
	rgn_2 = DataTableGetRegion( true, rgn_2_name );

	// remove existing links for the regions
	libwyv_region_remove_links(rgn_1_name, EventPlayer());
	libwyv_region_remove_links(rgn_2_name, EventPlayer());

	// create link between these two regions
	libwyv_link_regions(rgn_1_name, rgn_2_name);

	t = TriggerCreate("libwyv_region_entered");
	TriggerAddEventUnitRegion( t, null, rgn_1, true );
	TriggerAddEventUnitRegion( t, null, rgn_1, false );
	TriggerAddEventUnitRegion( t, null, rgn_2, true );
	TriggerAddEventUnitRegion( t, null, rgn_2, false );

	libwyv_print_output("Teleport created between regions " + libwyv_region_get_public_name(rgn_1_name)
	                    + " and " + libwyv_region_get_public_name(rgn_2_name), EventPlayer());

	return true;
}

// libwyv_command_region_attach
// =================
// attaches a region to a unit
bool libwyv_command_region_attach(bool checkConds, bool runActions) {
	string name = StringWord(libwyv_player[EventPlayer()].chat_msg, 2);
	region rgn = null;
	unitgroup g = libwyv_player[EventPlayer()].context;
	point offset;
	int waygate_dialog; // waygate dialog for region

	name = libwyv_region_get_name(name, EventPlayer());

	rgn = DataTableGetRegion(true, name);
	if(rgn == null) {
		libwyv_print_error("No region with that name exists!", EventPlayer());
		return true;
	}

	offset = Point( StringToFixed(StringWord(libwyv_player[EventPlayer()].chat_msg, 3)),
					StringToFixed(StringWord(libwyv_player[EventPlayer()].chat_msg, 4)) );

	if(UnitGroupCount(g, c_unitCountAll) == 0) {	// no units selected -> unattach region
		RegionAttachToUnit(rgn, null, offset);
		DataTableValueRemove(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + name);
		libwyv_print_success("Region unattached successfully.", EventPlayer());
	}
	else if(UnitGroupCount(g, c_unitCountAll) == 1) { // attach region to unit
		// if a unit has previously had region attached, remove unit custom value
		// set to track waygate dialogs (if it exists)
		if( DataTableValueExists(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + name) ) {
			UnitSetCustomValue(DataTableGetUnit(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + name), LIBWYV_UNIT_INDEX_WAYGATE_DIALOG, 0);
		}


		// attach region to new unit
		RegionAttachToUnit(rgn, UnitGroupUnit(g, 1), offset);
		DataTableSetUnit(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + name, UnitGroupUnit(g, 1));

		// if region has assocated waygate dialog, attach it to the unit as well
		if( DataTableValueExists(true, LIBWYV_WAYGATE_DIALOG_DATA_TABLE_IDENTIFIER + " " + name) ) {
			waygate_dialog = DataTableGetDialog(true, LIBWYV_WAYGATE_DIALOG_DATA_TABLE_IDENTIFIER + " " + name);
			DialogSetPositionRelativeToUnit(waygate_dialog, UnitGroupUnit(g, 1), "Ref_Origin", -60, 0);
			UnitSetCustomValue(UnitGroupUnit(g, 1), LIBWYV_UNIT_INDEX_WAYGATE_DIALOG, IntToFixed(waygate_dialog));

			// if a static anchor for waygate exists, remove it
			if( DataTableValueExists(true, LIBWYV_WAYGATE_ANCHOR_DATA_TABLE_IDENTIFIER + " " + name) ) {
				UnitRemove(DataTableGetUnit(true, LIBWYV_WAYGATE_ANCHOR_DATA_TABLE_IDENTIFIER + " " + name));
				DataTableValueRemove(true, LIBWYV_WAYGATE_ANCHOR_DATA_TABLE_IDENTIFIER + " " + name);
			}
		}

		libwyv_print_success("Region attached successfully.", EventPlayer());
	}
	else {
		libwyv_print_error("You can't attach the same region to multiple units!", EventPlayer());
	}

	return true;
}
