//=========================================================
//		Wyvern Roleplaying Engine
//		Copyright (c) 2014 , Xethyr.407 [NA] / @nmosfet
//=========================================================
// This software is distributed under the MIT License:
// <http://opensource.org/licenses/MIT>
//=========================================================

// libwyv_command_region_create
// ====================
// creates a region and sets it into the global data table
// regions are stored in format:
// "reg [player] [name]"
void libwyv_command_region_create(int user, unitgroup u, string argv) {
	region r;
	fixed radius;
	string name = StringWord(argv, 2);

	if( name == "" ) {
		libwyv_print_error(user, "Invalid region name");
		return;
	}

	radius = StringToFixed(StringWord(argv, 3));
	if( radius == 0.0 ) {
		radius = 2;
	}

	if( DataTableGetRegion( true, libwyv_region_get_name(name, user) ) ) {
		libwyv_print_error(user, "A region with that name already exists!");
		return;
	}

	r = RegionCircle( libwyv_player[user].mouse_position, radius );

	DataTableSetRegion( true, libwyv_region_get_name(name, user), r );
	libwyv_print_output(user, "Region (circle) \"" + name + "\" has been created.");

	return;
}

// libwyv_command_region_remove
// ==================
// removes a region and any linking to other regions
// (for linking, see libwyv_command_region_teleport below)
void libwyv_command_region_remove(int user, unitgroup u, string argv) {
	string name = libwyv_region_get_name(StringWord(argv, 2), user);
	region rgn = DataTableGetRegion( true, name );

	if( rgn == null ) {
		libwyv_print_error(user, "Region does not exist");
		return;
	}

	// first remove any linking data (for teleports) related to this region
	libwyv_region_remove_links(name, user);

	// remove region
	DataTableValueRemove( true, name );

	// if region attached to unit, remove unit anchor data table entry
	if( DataTableValueExists(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + name) ) {
		DataTableValueRemove(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + name);
	}

	libwyv_print_output(user, "Region \"" + libwyv_region_get_public_name(name) + "\" has been removed");

	return;
}

// libwyv_command_region_removeall
// ======================
// removes all regions and links
void libwyv_command_region_removeall(int user, unitgroup u, string argv) {
	int index = 1;
	int removed = 0;
	string rgn_identifier = libwyv_region_get_name("", user);
	string name; // full region data table name

	while( index <= DataTableValueCount(true) ) {
		if( StringContains(DataTableValueName(true, index), rgn_identifier, c_stringBegin, c_stringNoCase) ) {
			name = DataTableValueName(true, index);
			libwyv_region_remove_links( name, user );
			DataTableValueRemove(true, name);
			// if region attached to unit, remove unit anchor data table entry
			if( DataTableValueExists(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + name) ) {
				DataTableValueRemove(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + name);
			}

			removed += 1;
		}
		index += 1;
	}

	if(removed > 0) {
		libwyv_print_output(user, "All your regions have been removed");
	}
	else {
		libwyv_print_output(user, "You have no regions to remove");
	}

	return;
}
// libwyv_command_region_list
// ======================
// lists a player's regions from the data table
// because region creation is not case sensitive,
// these will always be in lowercase
void libwyv_command_region_list(int user, unitgroup u, string argv) {
	int i = 1;
	string region_list = "";

	while( i < DataTableValueCount(true) ) {
		if( StringWord( DataTableValueName(true, i), 1 ) == LIBWYV_REGION_DATA_TABLE_IDENTIFIER &&
			StringToInt( StringWord( DataTableValueName(true, i), 2) ) == user ) {
			region_list = region_list + ", " + libwyv_region_get_public_name(DataTableValueName(true, i));
		}
		i += 1;
	}

	if( region_list != "" ) {
		region_list = StringSub(region_list, 3, StringLength(region_list)); // get rid of leading ", "
		libwyv_print_output(user, "Your regions:\n" + region_list);
	}
	else {
		libwyv_print_output(user, "You have no regions");
	}

	return;
}

// libwyv_command_region_teleport
// =========================
// creates a "waygate" between two regions, allowing (any) player
// to press a dialog button to teleport selected units between
// regions. (see engine/regions.galaxy to see how regions are linked,
// events/region_waygates.galaxy to see how waygates function)
// (heavily modified from Cortex engine)
void libwyv_command_region_teleport(int user, unitgroup u, string argv) {
	string rgn_1_name = libwyv_region_get_name(StringWord(argv, 2), user);
	string rgn_2_name = libwyv_region_get_name(StringWord(argv, 3), user);
	region rgn_1;
	region rgn_2;
	trigger t;

	if( !DataTableValueExists(true, rgn_1_name) || !DataTableValueExists(true, rgn_2_name) ) {
		libwyv_print_error(user, "One or more invalid regions were entered.");
		return;
	}

	rgn_1 = DataTableGetRegion( true, rgn_1_name );
	rgn_2 = DataTableGetRegion( true, rgn_2_name );

	// remove existing links for the regions
	libwyv_region_remove_links(rgn_1_name, user);
	libwyv_region_remove_links(rgn_2_name, user);

	// create link between these two regions
	libwyv_link_regions(rgn_1_name, rgn_2_name);

	t = TriggerCreate("libwyv_region_entered");
	TriggerAddEventUnitRegion( t, null, rgn_1, true );
	TriggerAddEventUnitRegion( t, null, rgn_1, false );
	TriggerAddEventUnitRegion( t, null, rgn_2, true );
	TriggerAddEventUnitRegion( t, null, rgn_2, false );

	libwyv_print_output(user, "Teleport created between regions " + libwyv_region_get_public_name(rgn_1_name)
	                    + " and " + libwyv_region_get_public_name(rgn_2_name));

	return;
}

// libwyv_command_region_attach
// =================
// attaches a region to a unit
void libwyv_command_region_attach(int user, unitgroup u, string argv) {
	string name = StringWord(argv, 2);
	region rgn = null;
	unitgroup g = libwyv_player[user].context;
	point offset;
	int waygate_dialog; // waygate dialog for region

	name = libwyv_region_get_name(name, user);

	rgn = DataTableGetRegion(true, name);
	if(rgn == null) {
		libwyv_print_error(user, "No region with that name exists!");
		return;
	}

	offset = Point( StringToFixed(StringWord(argv, 3)),
					StringToFixed(StringWord(argv, 4)) );

	if(UnitGroupCount(g, c_unitCountAll) == 0) {	// no units selected -> unattach region
		RegionAttachToUnit(rgn, null, offset);
		DataTableValueRemove(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + name);
		libwyv_print_success(user, "Region unattached successfully.");
	}
	else if(UnitGroupCount(g, c_unitCountAll) == 1) { // attach region to unit
		// if a unit has previously had region attached, remove unit custom value
		// set to track waygate dialogs (if it exists)
		if( DataTableValueExists(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + name) ) {
			UnitSetCustomValue(DataTableGetUnit(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + name), LIBWYV_UNIT_INDEX_WAYGATE_DIALOG, 0);
		}


		// attach region to new unit
		RegionAttachToUnit(rgn, UnitGroupUnit(g, 1), offset);
		DataTableSetUnit(true, LIBWYV_REGION_ANCHOR_DATA_TABLE_IDENTIFIER + " " + name, UnitGroupUnit(g, 1));

		// if region has assocated waygate dialog, attach it to the unit as well
		if( DataTableValueExists(true, LIBWYV_WAYGATE_DIALOG_DATA_TABLE_IDENTIFIER + " " + name) ) {
			waygate_dialog = DataTableGetDialog(true, LIBWYV_WAYGATE_DIALOG_DATA_TABLE_IDENTIFIER + " " + name);
			DialogSetPositionRelativeToUnit(waygate_dialog, UnitGroupUnit(g, 1), "Ref_Origin", -60, 0);
			UnitSetCustomValue(UnitGroupUnit(g, 1), LIBWYV_UNIT_INDEX_WAYGATE_DIALOG, IntToFixed(waygate_dialog));

			// if a static anchor for waygate exists, remove it
			if( DataTableValueExists(true, LIBWYV_WAYGATE_ANCHOR_DATA_TABLE_IDENTIFIER + " " + name) ) {
				UnitRemove(DataTableGetUnit(true, LIBWYV_WAYGATE_ANCHOR_DATA_TABLE_IDENTIFIER + " " + name));
				DataTableValueRemove(true, LIBWYV_WAYGATE_ANCHOR_DATA_TABLE_IDENTIFIER + " " + name);
			}
		}

		libwyv_print_success(user, "Region attached successfully.");
	}
	else {
		libwyv_print_error(user, "You can't attach the same region to multiple units!");
	}

	return;
}
